buildscript {
    ext {
        springBootVersion = "1.4.7.RELEASE"
    }
    repositories {
        jcenter()
        maven {
            url "https://dl.bintray.com/dreamscale/maven-public"
        }
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
        classpath "org.dreamscale:gradle-core:1.3.1"
    }
}

plugins {
    id "com.jfrog.bintray" version "1.7"
}

apply plugin: "spring-boot"
apply from: "gradle/postgres.gradle"

allprojects {
    ext {
        groovyVersion = "2.4.11"
        spockVersion = "1.1-groovy-2.4"
    }

    apply plugin: "org.dreamscale.core"
    apply plugin: "org.dreamscale.maven-publish-ext"

    repositories {
        mavenLocal()
        maven {
            url "https://dl.bintray.com/dreamscale/maven-public"
        }
        jcenter()
    }

    dependencies {
        compileOnly "org.projectlombok:lombok:1.16.18"

        mainTestCompile "org.dreamscale:common-test:0.1"
        mainTestCompileOnly "org.codehaus.groovy:groovy-all:${groovyVersion}"
    }
}

dependencies {
    compile project(path: "rest-client")
    compile "com.google.guava:guava:23.6-jre"
    compile "org.springframework.boot:spring-boot-starter-web"
    compile "org.springframework.boot:spring-boot-starter-actuator"
    compile "com.fasterxml.jackson.datatype:jackson-datatype-jsr310:2.6.3"
    compile("net.sf.dozer:dozer:5.5.1") {
        exclude group: "org.slf4j"
    }
    compile "io.springfox:springfox-swagger2:2.7.0"
    compile "io.springfox:springfox-swagger-ui:2.7.0"

    compile "org.springframework.boot:spring-boot-starter-data-jpa"
    compile "postgresql:postgresql:9.0-801.jdbc4"
    compile "org.liquibase:liquibase-core"

    sharedTestCompile project(path: "rest-client", configuration: "mainTestRuntime")
    sharedTestCompile "org.springframework:spring-test"
    sharedTestCompile "org.springframework.boot:spring-boot-starter-test"
    sharedTestCompile "org.codehaus.groovy:groovy-all:${groovyVersion}"
    sharedTestCompile "cglib:cglib-nodep:3.2.0"
    sharedTestCompile("org.spockframework:spock-core:${spockVersion}") {
        exclude group: "org.codehaus.groovy"
    }
    sharedTestCompile("org.spockframework:spock-spring:${spockVersion}") {
        exclude group: "org.codehaus.groovy"
    }
}

bintray {
    user = project.hasProperty('bintray.user') ? project.property('bintray.user') : System.getenv('BINTRAY_USER')
    key = project.hasProperty('bintray.key') ? project.property('bintray.key') : System.getenv('BINTRAY_API_KEY')

    publications = ['htmFlow']
    pkg {
        repo = 'maven-public'
        userOrg = 'dreamscale'
        name = project.group + ':' + project.ext['artifactId']
        desc = project.description
        licenses = ['Apache-2.0']
        websiteUrl = 'https://github.com/dreamscale/htm_flow'
        issueTrackerUrl = 'https://github.com/dreamscale/htm_flow/issues'
        vcsUrl = 'https://github.com/dreamscale/htm_flow.git'
        licenses = ['Apache-2.0']
    }
}
